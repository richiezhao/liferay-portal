import com.liferay.gradle.util.copy.StripPathSegmentsAction

task buildSVG4Everybody(type: Copy)

File jsDestinationDir = file("tmp/META-INF/resources")

buildSVG4Everybody {
	dependsOn npmInstall
	eachFile new StripPathSegmentsAction(2)

	filter {
		String line ->

		line.replace "\"function\" == typeof define", "false && \"function\" == typeof define"
	}

	from npmInstall.nodeModulesDir
	include "svg4everybody/dist/svg4everybody.js"
	includeEmptyDirs = false
	into new File(jsDestinationDir, "svg4everybody")
}

classes {
	dependsOn buildSVG4Everybody
}

clean {
	delete "tmp"
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.core", version: "6.0.0"
	compileOnly project(":apps:portal-url-builder:portal-url-builder-api")
}
@component-name = "portal-collaboration"
definition {

	property osgi.module.configuration.file.names = "com.liferay.translation.internal.configuration.FFTranslationWorkflowConfiguration.config";
	property osgi.module.configurations = "enabled=B&quot;true&quot;";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Translations Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "This ensures that a new version of a published WC can be created and submitted for publication once a new translation is approved."
	@priority = "5"
	test ApproveNewTranslationForPublishedWC {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Guest",
			title = "WC WebContent Title");

		Workflow.openWorkflowAdmin(siteURLKey = "guest");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Translation");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Administrator",
			userScreenName = "usersn");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		Translations.gotoTranslate(webContentTitle = "WC WebContent Title");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "es-ES");

		Translations.translateFields(
			webContentContent = "WC WebContent Contenido",
			webContentTitle = "WC WebContent Título");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Alert.viewSuccessMessage();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		PortletEntry.changeLocale(locale = "es-ES");

		WebContent.viewCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to es-ES",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to es-ES",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		PortletEntry.changeLocale(locale = "es-ES");

		WebContent.viewCP(
			webContentContent = "WC WebContent Contenido",
			webContentTitle = "WC WebContent Título");
	}

	@description = "This ensures that a rejected translation does not appear in a WC."
	@priority = "5"
	test RejectTranslationsForDraftAndApprovedWC {
		for (var resourceValue : list "Translation,Web Content Article") {
			Workflow.openWorkflowAdmin(siteURLKey = "guest");

			Workflow.configureWorkflow(
				workflowDefinition = "Single Approver",
				workflowResourceValue = "${resourceValue}");
		}

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithWorkflowCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		Translations.gotoTranslate(webContentTitle = "WC WebContent Title");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "es-ES");

		Translations.translateFields(
			webContentContent = "WC WebContent Contenido",
			webContentTitle = "WC WebContent Título");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Alert.viewSuccessMessage();

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to es-ES",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.rejectTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to es-ES",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		PortletEntry.changeLocale(locale = "es-ES");

		WebContent.viewCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "WC WebContent Title",
			workflowAssetType = "Web Content Article",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "WC WebContent Title",
			workflowAssetType = "Web Content Article",
			workflowTask = "Review");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		Translations.gotoTranslate(webContentTitle = "WC WebContent Title");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "ja-JP");

		Translations.translateFields(
			webContentContent = "wc webcontentコンテンツ",
			webContentTitle = "wc webcontentタイトル");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Alert.viewSuccessMessage();

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to ja-JP",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.rejectTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to ja-JP",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		PortletEntry.changeLocale(locale = "ja-JP");

		WebContent.viewCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");
	}

	@description = "This ensures that a user cannot submit a translation with the same language that is pending review."
	@priority = "5"
	test UpdateSameTranslationAsAnotherUser {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Guest",
			title = "WC WebContent Title");

		Workflow.openWorkflowAdmin(siteURLKey = "guest");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Translation");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		Translations.gotoTranslate(webContentTitle = "WC WebContent Title");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "es-ES");

		Translations.translateFields(
			webContentContent = "WC WebContent Contenido",
			webContentTitle = "WC WebContent Título");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Alert.viewSuccessMessage();

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.assignRegularRoleCP(
			roleTitle = "Administrator",
			userScreenName = "usersn");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		Translations.gotoTranslate(webContentTitle = "WC WebContent Title");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "zh-CN");

		Translations.translateFields(
			webContentContent = "WC WebContent 内容",
			webContentTitle = "WC WebContent 名称");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Alert.viewSuccessMessage();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		Translations.gotoTranslate(webContentTitle = "WC WebContent Title");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "es-ES");

		AssertElementPresent(locator1 = "Button#SUBMIT_FOR_PUBLICATION_DISABLED");
	}

	@description = "This ensures that a pending WC can be updated with a newly added translation once the translation is approved."
	@priority = "5"
	test ViewUpdatedWCSubmissionAfterApprovedTranslation {
		for (var resourceValue : list "Translation,Web Content Article") {
			Workflow.openWorkflowAdmin(siteURLKey = "guest");

			Workflow.configureWorkflow(
				workflowDefinition = "Single Approver",
				workflowResourceValue = "${resourceValue}");
		}

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithWorkflowCP(
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		Translations.gotoTranslate(webContentTitle = "WC WebContent Title");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "es-ES");

		Translations.translateFields(
			webContentContent = "WC WebContent Contenido",
			webContentTitle = "WC WebContent Título");

		AssertClick(
			locator1 = "Button#SUBMIT_FOR_PUBLICATION",
			value1 = "Submit for Publication");

		Alert.viewSuccessMessage();

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to es-ES",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Translation of WC WebContent Title to es-ES",
			workflowAssetType = "Translation",
			workflowTask = "Review");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC WebContent Title");

		PortletEntry.changeLocale(locale = "es-ES");

		WebContent.viewCP(
			webContentContent = "WC WebContent Contenido",
			webContentTitle = "WC WebContent Título");
	}

}
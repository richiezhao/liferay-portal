definition {

	var userEmailAddress = "test@liferay.com";
	var userPassword = "test";
	var userPasswordReminderAnswer = "test";

	macro _signIn {
		if (IsElementNotPresent(locator1 = "TextInput#EMAIL_ADDRESS")) {
			AssertClick(
				locator1 = "UserBar#USER_SIGN_IN",
				value1 = "Sign In");
		}

		Type.typePause(
			locator1 = "TextInput#EMAIL_ADDRESS",
			value1 = "${userEmailAddress}");

		Type.typePause(
			locator1 = "TextInput#PASSWORD",
			value1 = "${userPassword}");

		if ("${rememberMe}" == "true") {
			Check(locator1 = "Checkbox#REMEMBER_ME");
		}

		AssertClick(
			locator1 = "Button#SIGN_IN",
			value1 = "Sign In");

		AssertElementNotPresent(locator1 = "Button#SIGN_IN");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	@summary = "Agree to Liferay Portal Terms of Use"
	macro agreeToTermsOfUse {
		AssertClick(
			locator1 = "Button#I_AGREE",
			value1 = "I Agree");

		AssertElementNotPresent(locator1 = "Button#I_AGREE");
	}

	@summary = "Enter '${userPasswordReminderAnswer}' as password reminder"
	macro setPasswordReminder {
		Type(
			locator1 = "TextInput#ANSWER",
			value1 = "${userPasswordReminderAnswer}");

		Button.clickSave();
	}

	@summary = "Sign in to Liferay Portal as '${userEmailAddress}' using the password '${userPassword}'"
	macro signIn {
		Navigator.openURL();

		SignIn._signIn(
			rememberMe = "${rememberMe}",
			userEmailAddress = "${userEmailAddress}",
			userPassword = "${userPassword}");
	}

	macro signInPortalInstanceSetUp {
		var defaultPortalURL = PropsUtil.get("default.portal.url");

		SignIn.signInSpecificURL(url = "${defaultPortalURL}");

		if (IsElementPresent(locator1 = "Button#I_AGREE")) {
			SignIn.agreeToTermsOfUse();
		}

		if (IsElementPresent(locator1 = "Select#QUESTION")) {
			SignIn.setPasswordReminder();
		}

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		while (IsElementPresent(locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_ELLIPSIS", key_rowNumber = "2")) {
			Click.waitForMenuToggleJSClick(
				key_rowNumber = "2",
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_ELLIPSIS");

			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}

		var portalURL = PropsUtil.get("portal.url");
		var portalURL = StringUtil.replace("${portalURL}", "http://", "");
		var portalURL = StringUtil.replace("${portalURL}", ":8080", "");

		LexiconEntry.gotoAdd();

		WaitForElementPresent(locator1 = "//script[contains(@src,'ManagementToolbar.es.js')]");

		Type(
			locator1 = "TextInput#WEB_ID",
			value1 = "${portalURL}");

		Type(
			locator1 = "TextInput#VIRTUAL_HOST",
			value1 = "${portalURL}");

		Type(
			locator1 = "TextInput#MAIL_DOMAIN",
			value1 = "liferay.com");

		Button.clickSave();

		Pause(locator1 = "3000");

		WaitForElementPresent(locator1 = "//script[contains(@src,'OpenToast.es.js')]");

		Navigator.openURL();
	}

	macro signInSpecificURL {
		Navigator.openSpecificURL(url = "${url}");

		SignIn._signIn(
			rememberMe = "${rememberMe}",
			userEmailAddress = "${userEmailAddress}",
			userPassword = "${userPassword}");
	}

	@summary = "Sign in to Liferay Portal as '${userEmailAddress}' using the password '${userPassword}'"
	macro signInTestSetup {
		SignOut.signOut();

		SignIn.signIn();

		if (IsElementPresent(locator1 = "Button#I_AGREE")) {
			SignIn.agreeToTermsOfUse();
		}

		if (IsElementPresent(locator1 = "Select#QUESTION")) {
			SignIn.setPasswordReminder();
		}
	}

}
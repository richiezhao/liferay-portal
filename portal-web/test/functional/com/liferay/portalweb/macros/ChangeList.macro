definition {

	macro assertDiscardIsUnavailableForInactivePublication {
		Click(
			key_publicationName = "${publicationName}",
			locator1 = "ChangeList#PUBLISHED_PUBLICATIONS_NAME");

		Click(
			key_changeCategory = "${changeCategory}",
			key_changeTitle = "${changeTitle}",
			locator1 = "ChangeListChanges#CHANGES_CHANGE_TITLE");

		AssertElementNotPresent(locator1 = "ChangeListChanges#CHANGES_ELLIPSIS");
	}

	macro cancelPublicationCreation {
		ApplicationsMenu.gotoPortlet(
			category = "Publications",
			panel = "Applications",
			portlet = "Publications");

		Click(locator1 = "Button#PLUS");

		Type(
			locator1 = "ChangeList#PUBLICATION_NAME",
			value1 = "${publicationName}");

		Button.clickCancel();
	}

	macro createNewPublication {
		if ("${gotoManagePublications}" == "true") {
			ApplicationsMenu.gotoPortlet(
				category = "Publications",
				panel = "Applications",
				portlet = "Publications");

			Click(locator1 = "Button#PLUS");
		}
		else {
			Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

			MenuItem.click(menuItem = "Create New Publication");
		}

		if (isSet(publicationName)) {
			Type(
				locator1 = "ChangeList#PUBLICATION_NAME",
				value1 = "${publicationName}");
		}

		if (isSet(publicationDescription)) {
			Type(
				locator1 = "ChangeList#PUBLICATION_DESCRIPTION",
				value1 = "${publicationDescription}");
		}

		Button.click(button = "Create");

		Pause(locator1 = "5000");
	}

	macro deletePublication {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		ClickNoError(
			key_menuItem = "Delete",
			locator1 = "MenuItem#ANY_MENU_ITEM");

		AssertConfirm(value1 = "Are you sure you want to delete this publication? You will permanently lose the changes in this publication. You will not be able to undo this operation.");

		AssertTextEquals(
			locator1 = "ChangeList#MANGE_PUBLICATIONS_PAGE_TITLE",
			value1 = "Publications");

		Alert.viewSuccessMessage();
	}

	macro disablePublications {
		Uncheck.uncheckToggleSwitch(locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH");

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro discardChanges {
		if ("${conflictingChanges}" == "true") {
			Click(
				key_entryTitle = "${entryTitle}",
				locator1 = "ChangeList#CONFICTING_CHANGES_DISCARD_BUTTON",
				value1 = "Discard");
		}
		else {
			Click(
				key_changeCategory = "${changeCategory}",
				key_changeTitle = "${changeTitle}",
				locator1 = "ChangeListChanges#CHANGES_CHANGE_TITLE");

			Click(locator1 = "ChangeListChanges#CHANGES_ELLIPSIS");

			MenuItem.click(menuItem = "Discard");
		}

		Pause(locator1 = "3000");

		AssertElementPresent(locator1 = "ChangeList#DISCARDED_CHANGES_TITLE");

		key_changeCategory = "${changeCategory}";
		key_changeTitle = "${changeTitle}";

		AssertTextEquals.assertPartialText(
			locator1 = "ChangeListChanges#CONTEXT_CHANGE_TITLE",
			value1 = "${changeTitle}");

		Click(
			key_text = "Discard",
			locator1 = "Button#ANY");

		Alert.viewSuccessMessage();
	}

	macro enablePublications {
		Pause(locator1 = "2000");

		Check.checkToggleSwitch(locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH");

		if (isSet(gotoOverview)) {
			Click(locator1 = "ChangeListSettings#SAVE_AND_GO_TO_OVERVIEW_BUTTON");
		}
		else {
			Click(locator1 = "Button#SUBMIT");
		}

		Alert.viewSuccessMessage();
	}

	macro gotoReviewChanges {
		Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

		MenuItem.click(menuItem = "Review Changes");

		Pause(locator1 = "3000");
	}

	macro gotoViewDiffInConflictingChanges {
		Click(
			key_entryTitle = "${entryTitle}",
			locator1 = "ChangeList#CONFICTING_CHANGES_VIEW_BUTTON",
			value1 = "View");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		SelectFrameTop(value1 = "relative=top");

		AssertElementPresent(
			key_iframeTitle = "${entryTitle}",
			locator1 = "IFrame#IFRAME_TITLE");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	macro prepareToPublish {
		if ("${gotoManagePublications}" == "true") {
			ApplicationsMenu.gotoPortlet(
				category = "Publications",
				panel = "Applications",
				portlet = "Publications");

			LexiconEntry.changeDisplayStyle(displayStyle = "list");

			Click(
				key_publicationName = "${publicationName}",
				locator1 = "ChangeList#LIST_PUBLICATION_ELLIPSIS");
		}
		else {
			Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");
		}

		MenuItem.click(menuItem = "Prepare to Publish");
	}

	macro publishPublication {
		Click(locator1 = "ChangeList#PUBLISH_BUTTON");

		Alert.viewSuccessMessage();

		var key_navBarTitle = "History";

		AssertTextEquals(
			locator1 = "ChangeList#MANGE_PUBLICATIONS_NAVBAR_TITLE",
			value1 = "History");

		// Workaround LPS-115320

		Refresh();

		var key_publicationName = "${publicationName}";

		AssertTextEquals(
			locator1 = "ChangeList#HISTORY_PUBLICATION_STATUS",
			value1 = "Published");
	}

	macro revertPublication {
		var key_publicationName = "${publicationName}";

		Click(locator1 = "ChangeList#REVERT_BUTTON");

		AssertTextEquals(
			locator1 = "ChangeList#PUBLICATION_NAME",
			value1 = "Revert &quot;${publicationName}&quot;");

		if ("${publishNow}" == "true") {
			Click(
				key_radioName = "Now",
				locator1 = "ChangeList#REVERT_SCREEN_RADIO_OPTION",
				value1 = "Now");

			AssertTextEquals.assertPartialText(
				key_radioHelpMessage = "Revert your changes to production immediately",
				locator1 = "ChangeList#REVERT_SCREEN_RADIO_HELP",
				value1 = "Revert your changes to production immediately");
		}
		else {
			Click(
				key_radioName = "Later",
				locator1 = "ChangeList#REVERT_SCREEN_RADIO_OPTION",
				value1 = "Later");

			AssertTextEquals.assertPartialText(
				key_radioHelpMessage = "Make additional changes and publish them when you are ready",
				locator1 = "ChangeList#REVERT_SCREEN_RADIO_HELP",
				value1 = "Make additional changes and publish them when you are ready");
		}

		Button.click(button = "Revert and Create Publication");

		Alert.viewSuccessMessage();

		AssertTextPresent(
			key_publicationName = "Revert &quot;${publicationName}&quot;",
			locator1 = "ChangeList#CHANGE_LIST_INDICATOR_TITLE",
			value1 = "Revert &quot;${publicationName}&quot;");
	}

	macro search {
		Search.searchCP(searchTerm = "${publicationName}");

		key_publicationName = "${publicationName}";

		AssertTextEquals(
			locator1 = "ChangeList#PUBLISHED_PUBLICATIONS_NAME",
			value1 = "${publicationName}");
	}

	macro selectPublication {
		Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

		MenuItem.click(menuItem = "Select a Publication");

		SelectFrame(locator1 = "IFrame#SELECT_PUBLICATION");

		var key_publicationName = "${publicationName}";

		Click(
			locator1 = "ChangeList#SELECT_PUBLICATION_NAME",
			value1 = "${publicationName}");

		AssertTextPresent(
			locator1 = "ChangeList#CHANGE_LIST_INDICATOR_TITLE",
			value1 = "${publicationName}");
	}

	macro viewAutomaticallyResolved {
		AssertElementPresent(locator1 = "ChangeList#AUTOMATICALLY_RESOLVED_TABLE");

		AssertElementPresent(
			key_entryTitle = "${entryTitle}",
			key_successMessage = "${successMessage}",
			locator1 = "ChangeList#CONFICTING_CHANGES_SUCCESS_CONTENT");
	}

	macro viewChangesDiff {
		if (contains("${entryTitle}", "modified")) {
			AssertTextEquals.assertPartialText(
				key_publicationName = "Production",
				locator1 = "ChangeList#PUBLICATION_NAME_IN_DIFF",
				value1 = "Production");

			AssertTextEquals.assertPartialText(
				key_publicationName = "${publicationName}",
				locator1 = "ChangeList#PUBLICATION_NAME_IN_DIFF",
				value1 = "${publicationName}");

			key_columnName = "${columnName}";
			key_columnValueInProduction = "${columnValueInProduction}";
			key_columnValueInPublication = "${columnValueInPublication}";

			if (isSet(columnValueInProduction)) {
				AssertTextEquals.assertPartialText(
					locator1 = "ChangeList#COLUMN_VALUE_IN_PRODUCTION",
					value1 = "${columnValueInProduction}");
			}

			AssertTextEquals.assertPartialText(
				locator1 = "ChangeList#COLUMN_VALUE_IN_PUBLICATION",
				value1 = "${columnValueInPublication}");
		}
	}

	macro viewNeedsManualResolution {
		AssertElementPresent(locator1 = "ChangeList#NEEDS_MANUAL_RESOLUTION_TABLE");

		AssertElementPresent(
			key_entryTitle = "${entryTitle}",
			key_warningMessage = "${warningMessage}",
			locator1 = "ChangeList#CONFICTING_CHANGES_WARNING_CONTENT");

		AssertVisible(locator1 = "ChangeList#DISABLED_PUBLISH_BUTTON");
	}

	macro viewNoStagingMenuitemsNotAvailable {
		AssertElementNotPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "Staging",
			site = "${site}");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Staging and change lists cannot be used together.");
	}

	macro viewPublishedPublicationDetails {
		key_publicationName = "${publicationName}";

		AssertTextEquals(
			locator1 = "ChangeList#PUBLISHED_PUBLICATIONS_NAME",
			value1 = "${publicationName}");

		AssertElementPresent(
			key_publicationPublishedBy = "${publicationPublishedBy}",
			locator1 = "ChangeList#PUBLICATIONS_PUBLISHED_BY");
	}

	macro viewWarningOnPortletExport {
		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "${portletName}",
			site = "${site}");

		LAR._gotoExportImport();

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");

		IFrame.closeFrame();
	}

	macro viewWarningOnPortletImport {
		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "${portletName}",
			site = "${site}");

		LAR._gotoExportImport();

		SelectFrame(locator1 = "IFrame#CONFIGURATION_DIALOG");

		NavItem.click(navItem = "Import");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");

		IFrame.closeFrame();
	}

	macro viewWarningOnSiteExportImport {
		ProductMenu.gotoPortlet(
			category = "Publishing",
			portlet = "${portletName}",
			site = "${site}");

		Alert.viewWarningSpecific(warningMessage = "Change lists are enabled for the current virtual instance. Export/Import and change lists cannot be used together.");
	}

	macro workOnProduction {
		Click(locator1 = "ChangeList#CHANGE_LIST_INDICATOR_BUTTON");

		MenuItem.click(menuItem = "Work on Production");
	}

}